buildscript {
  apply plugin: 'hyperskill'

  wrapper {
    gradleVersion = '8.1'
  }

  repositories {
    maven { url 'https://packages.jetbrains.team/maven/p/academy/hyperskill-hspc' }
    maven { url 'https://jitpack.io' }
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.hyperskill:hs-gradle-plugin:release-SNAPSHOT'
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }
}

plugins {
  id 'com.android.application' version '7.3.0' apply false
  id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
}

allprojects {
  apply plugin: 'hyperskill'

  afterEvaluate {
    if (extensions.findByName('android') != null) {
      android {
        testOptions {
          unitTests {
            includeAndroidResources = true
            all {
              afterTest { TestDescriptor test, TestResult result ->
                if (result.resultType == TestResult.ResultType.FAILURE) {
                  def message = result.exception?.message ?: "Wrong answer"
                  def lines = message.readLines()
                  println "#educational_plugin FAILED + " + lines[0]
                  lines.subList(1, lines.size()).forEach { line ->
                    println "#educational_plugin" + line
                  }
                  // we need this to separate output of different tests
                  println()
                }
              }
            }
          }
        }

        defaultConfig {
          testInstrumentationRunner 'com.edu.AndroidEduTestRunner'
        }
      }

      dependencies {
        def robolectric = hs.android.robolectricVersion
        testImplementation "org.robolectric:robolectric:$robolectric"

        def junit = hs.android.lib.junit
        testImplementation "junit:junit:$junit"
      }
    }
  }
}